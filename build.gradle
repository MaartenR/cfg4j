/*
 * Copyright 2015 Norbert Potocki (norbert.potocki@nort.pl)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
// ----------- Build script configuration -----------

buildscript {

    ext {
        artifactGroup = "nl.serviceplanet.cfg4j"
        artifactVersion = "4.4.2"
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath group: "com.github.ben-manes", name: "gradle-versions-plugin", version: "0.12.0"
    }
}

// Generate Gradle wrapper
task wrapper(type: Wrapper) {
    gradleVersion = "2.13"
}

subprojects {

    // ----------- Plugins -----------
    apply plugin: "java"
    apply plugin: "idea"
    apply plugin: "jacoco"
    apply plugin: "maven"
    apply plugin: "com.github.ben-manes.versions"

    // ----------- Repositories -----------
    repositories {
        mavenLocal()
        mavenCentral()
    }

    // ----------- External module dependencies -----------
    dependencies {

        // Logging
        compile group: "org.slf4j", name: "slf4j-api", version: "1.7.21"

        // YAML
        compile group: "org.yaml", name: "snakeyaml", version: '1.17'

        // Metrics
        compile group: "io.dropwizard.metrics", name: "metrics-core", version: "3.1.2"

        testCompile group: "junit", name: "junit", version: "4.12"
        testCompile group: "org.assertj", name: "assertj-core", version: "2.4.1"
        testCompile group: "org.mockito", name: "mockito-core", version: "1.10.19"
        testCompile group: "org.powermock", name: "powermock-api-mockito", version: "1.6.4"
        testCompile group: "org.powermock", name: "powermock-module-junit4", version: "1.6.4"
    }

    // ----------- EXTRA tasks -----------
    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

// ----------- Task configurations -----------
    defaultTasks "clean", "build"

    compileJava {
        sourceCompatibility = 1.7
        targetCompatibility = 1.7
    }

    jacocoTestReport {
        reports {
            xml.enabled false
            html.enabled false
        }
    }

    group = "${artifactGroup}"
    version = "${artifactVersion}"

    artifacts {
        archives javadocJar, sourcesJar
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { }

                def nexusUsername = project.findProperty("nexusUsername") ?: ""
                def nexusPassword = project.findProperty("nexusPassword") ?: ""

                repository(url: "https://maven.serviceplanet.nl/content/repositories/releases/") {
                    authentication(userName: nexusUsername, password: nexusPassword)
                }

                snapshotRepository(url: "https://maven.serviceplanet.nl/content/repositories/snapshots/") {
                    authentication(userName: nexusUsername, password: nexusPassword)
                }

                pom.project {
                    name 'cfg4j'
                    packaging 'jar'
                    description 'Configuration library for Java distributed apps.'
                    url 'http://cfg4j.org'

                    scm {
                        url 'git@github.com:MaartenR/cfg4j.git'
                        connection 'scm:git:git@github.com:MaartenR/cfg4j.git'
                        developerConnection 'scm:git:git@github.com:MaartenR/cfg4j.git'
                    }


                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'norbertpotocki'
                            name 'Norbert Potocki'
                            email 'norbert.potocki@nort.pl'
                        }
                    }
                }
            }
        }
    }

}